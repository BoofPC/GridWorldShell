plugins {
    id 'eclipse'
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.6'
    id 'distribution'
}

import io.franzbecker.gradle.lombok.task.DelombokTask

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

lombok {
    version = '1.16.8'
    sha256 = 'fe32b29b7b33eb2b19866cbc5345c59c9984a6036cc396856a04c0d09f99ea68'
}

dependencies {
    compile name: 'gridworld'

    testCompile 'junit:junit:4.12'
}

jar {
    exclude 'CashGrabRunner*'
    // exclude 'cashgrab/*'
}

javadoc {
    source = 'src/delombok/java'
    exclude '**/*.gif'
    exclude 'CashGrabRunner*'
    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
    }
}

task delombok(type: DelombokTask) {
    args "src/main/java", '-d', "src/delombok/java"
}

task delombokHelp(type: DelombokTask) {
    args '--help'
}

javadoc.dependsOn delombok

task javadocJar(type: Zip, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    appendix = 'javadoc'
    extension = 'jar'
}

distributions {
    main {
        contents {
            from('build/libs/gridworld-shell.jar',
                 'build/distributions/gridworld-shell-javadoc.jar',
                 'src/delombok/java/CashGrabRunner.java')
            with {	
                from 'src/delombok/java/info/gridworld/cashgrab/CalebBug.java'
                filter(org.apache.tools.ant.filters.HeadFilter, lines:-1, skip:2)
            }
        }
    }
}

distTar.compression = Compression.GZIP
distTar.extension = "tar.gz"

[distZip, distTar]*.dependsOn(jar, delombok, javadocJar)
